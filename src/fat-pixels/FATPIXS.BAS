0 '*************  Fatpixs, Markle, 12/26/84  **************
1 '*  Copyright 1984 (c) by CompuQuill, Atlanta, Georgia  *
2 '********************************************************
5 KEY OFF:FOR I=1 TO 10:KEY I,"":NEXT:GOTO 10' Disable soft keys
7 SAVE"FATPIXS":END
9 '******************* Initialization  ********************
10 DEFINT A-Z:SCREEN 0,0,0:WIDTH 80:COLOR 7,0:CLS:CURSOR=43:TMPCLR=15:TMPBCK=0
20 DIM CLRTAB(15),DENSITY(2),DENSTR$(3),BCKGRD$(1),BCKGRD(1),SAVCLR(65),SAVBCK(65),CHAR(65),ROW(65),COL(65)
30 PIXSIZ=1:PIXMAX=64:COLMAX=79:ROWMAX=25:COL=PIXMAX\2:COL=COL-(COL MOD PIXSIZ)+1:ROW=ROWMAX\2
40 DATA 0,8,7,15,5,13,1,9,11,3,10,2,14,6,12,4
50 FOR I=0 TO 15:READ CLRTAB(I):NEXT:CURCLR=3' Read in color table
60 DENSITY(0)=219:DENSITY(1)=177:DENSITY(2)=176:DENSITY$=CHR$(219)+CHR$(177)+CHR$(219)
70 DENSTR$(0)=" 100":DENSTR$(1)=" 50":DENSTR$(2)=" 25":DENSITY=0:HIGH=3:LOW=6
80 BCKGRD$(0)=" Black ":BCKGRD$(1)="White":BCKGRD(0)=0:BCKGRD(1)=7:BCKGRD=0
90 FILDRV$="Z":NAMFIL$="PICTURE":GOSUB 3000:GOSUB 30000:ON ERROR GOTO 26000' Display control panel
499 '*********************  Mainline  **********************
500 FOR M=1 TO 60:KB$=INKEY$:IF KB$="" THEN 540' Check keyboard
510 KB=ASC(RIGHT$(KB$,1)):IF KB<>53 THEN GOSUB 2000:GOTO 500' Select
520 IF COL>PIXMAX THEN GOSUB 1000:GOTO 500
530 Z=65:GOSUB 20000:ROW(Z)=ROW:COL(Z)=COL:Z=0:GOSUB 13000:SAVED=0:LASTROW=ROW:LASTCOL=COL:GOTO 500
540 GOSUB 600:NEXT:GOTO 500
600 GOSUB 20000:COLOR TMPCLR,TMPBCK:GOSUB 12000:PRINT CHR$(CURSOR);:FOR D=1 TO 60:NEXT:GOSUB 21000:RETURN' Blinking cursor
999 '************  Select control panel options  ************
1000 IF ROW<17 THEN CURCLR=ROW-1:GOSUB 3000:RETURN
1010 ON ROW-16 GOSUB 1100,1200,1300,1400,1500,1600,1020,1020,1700:GOSUB 3000:RETURN
1020 BEEP:RETURN
1100 DENSITY=0:IF COL>COLMAX-5 THEN DENSITY=2:GOTO 1120' Density
1110 IF COL>COLMAX-9 THEN DENSITY=1
1120 RETURN
1200 IF COL>COLMAX-8 THEN BCKGRD=1 ELSE BCKGRD=0' Background
1210 RETURN
1300 GOSUB 33000:RETURN' Fill
1400 GOSUB 31000:RETURN' Clear
1500 GOSUB 34000:RETURN' Undo
1600 IF COL>COLMAX-5 THEN 2500' End
1610 IF COL>COLMAX-10 THEN GOSUB 22000:RETURN' Save
1620 GOSUB 23000:RETURN' Load
1700 GOSUB 30000:RETURN' Help
1999 '*************  Movement and Function keys  **************
2000 IF KB<10 THEN GOSUB 10000:RETURN' Jump moves
2010 IF KB>48 AND KB<58 THEN GOSUB 11000:GOSUB 600:RETURN' Cursor
2020 IF KB>58 AND KB<65 THEN ON KB-58 GOSUB 2200,2250,2300,2350,2400,2400:GOSUB 3000:RETURN' Control functions
2030 IF KB=102 OR KB=70 THEN LASTROW=ROW:LASTCOL=COL:GOSUB 33000:RETURN 'Fill
2040 IF KB=99  OR KB=67 THEN GOSUB 31000:RETURN' Clear
2050 IF KB=117 OR KB=85 THEN GOSUB 34000:RETURN' Undo
2060 IF KB=104 OR KB=72 THEN GOSUB 30000:RETURN' Help
2070 IF KB=108 OR KB=76 THEN GOSUB 23000:RETURN' Load
2080 IF KB=115 OR KB=83 THEN GOSUB 22000:RETURN' Save
2090 IF KB=101 OR KB=69 THEN 2500' End
2100 BEEP:RETURN
2200 CURCLR=CURCLR-1:IF CURCLR<0 THEN CURCLR=15' Color down
2210 RETURN
2250 CURCLR=CURCLR+1:IF CURCLR>15 THEN CURCLR=0' Color up
2260 RETURN
2300 DENSITY=DENSITY-1:IF DENSITY<0 THEN DENSITY=2' Density left
2310 RETURN
2350 DENSITY=DENSITY+1:IF DENSITY>2 THEN DENSITY=0' Density right
2360 RETURN
2400 IF BCKGRD=1 THEN BCKGRD=0 ELSE BCKGRD=1' Switch Background
2410 RETURN
2500 GOSUB 29000:ON ERROR GOTO 0:COLOR 7,0:CLS:END' Program exit
2999 '*********************************************************
3000 FOR I=0 TO 15:LOCATE I+1,PIXMAX+1:GOSUB 3500
3010 COLOR CLRTAB(I):PRINT STRING$(13,254);:GOSUB 3500:NEXT
3020 FOR I=17 TO 25:LOCATE I,PIXMAX+1:GOSUB 3500:LOCATE I,PIXMAX+15:GOSUB 3500:NEXT
3030 LOCATE 17,PIXMAX+2:FOR I=0 TO 2:IF DENSITY=I THEN COLOR HIGH ELSE COLOR LOW
3040 PRINT DENSTR$(I);"%";:NEXT
3050 LOCATE 18,PIXMAX+2:FOR I=0 TO 1:IF BCKGRD=I THEN COLOR HIGH ELSE COLOR LOW
3060 PRINT BCKGRD$(I);:NEXT:COLOR LOW
3070 LOCATE 19,PIXMAX+2:PRINT"*** ";:COLOR HIGH:PRINT"F";:COLOR LOW:PRINT"ill  ***"
3080 LOCATE 20,PIXMAX+2:PRINT"*** ";:COLOR HIGH:PRINT"C";:COLOR LOW:PRINT"lear ***"
3090 LOCATE 21,PIXMAX+2:PRINT"*** ";:COLOR HIGH:PRINT"U";:COLOR LOW:PRINT"ndo  ***"
3100 LOCATE 22,PIXMAX+2:COLOR HIGH:PRINT"L";:COLOR LOW:PRINT"oad ";:COLOR HIGH
3110 PRINT"S";:COLOR LOW:PRINT"ave ";:COLOR HIGH:PRINT"E";:COLOR LOW:PRINT"nd"
3120 LOCATE 23,PIXMAX+2:PRINT"Drive:";:COLOR HIGH:PRINT FILDRV$;"      ";:COLOR LOW
3130 LOCATE 24,PIXMAX+2:PRINT"Name:";:COLOR HIGH:PRINT NAMFIL$;SPACE$(8-LEN(NAMFIL$));:COLOR LOW
3140 LOCATE 25,PIXMAX+2:PRINT"*** ";:COLOR HIGH:PRINT"H";:COLOR LOW:PRINT"elp  ***";:GOSUB 12000:RETURN
3500 COLOR HIGH,0:IF I=CURCLR THEN COLOR HIGH+16:PRINT"*"; ELSE PRINT"|";
3510 RETURN
9999 '**********************  Jump moves  *********************
10000 ON KB GOTO 10080,10030,10070,10040,10100,10050,10090,10020,10060
10010 GOTO 12010
10020 ROW=1:COL=PIXMAX\2:COL=COL-(COL MOD PIXSIZ)+1:GOTO 12000' Up
10030 ROW=ROWMAX:COL=PIXMAX\2:COL=COL-(COL MOD PIXSIZ)+1:GOTO 12000' Down
10040 ROW=ROWMAX\2:COL=1:GOTO 12000' Left
10050 ROW=ROWMAX\2:COL=PIXMAX-PIXSIZ+1:GOTO 12000' Right
10060 ROW=1:COL=PIXMAX-PIXSIZ+1:GOTO 12000' Up & right
10070 ROW=ROWMAX:COL=PIXMAX-PIXSIZ+1:GOTO 12000' Down & right
10080 ROW=ROWMAX:COL=1:GOTO 12000' Down & left
10090 ROW=1:COL=1:GOTO 12000' Up & left
10100 ROW=ROWMAX\2:COL=PIXMAX\2:COL=COL-(COL MOD PIXSIZ)+1:GOTO 12000' Center
10999 '********************  Move cursor  *********************
11000 ON KB-48 GOTO 11080,11030,11070,11040,12010,11050,11090,11020,11060
11010 GOTO 12010
11020 GOSUB 11100:GOTO 12000' Up
11030 GOSUB 11120:GOTO 12000' Down
11040 GOSUB 11140:GOTO 12000' Left
11050 GOSUB 11160:GOTO 12000' Right
11060 GOSUB 11100:GOSUB 11160:GOTO 12000' Up & right
11070 GOSUB 11120:GOSUB 11160:GOTO 12000' Down & right
11080 GOSUB 11120:GOSUB 11140:GOTO 12000' Down & left
11090 GOSUB 11100:GOSUB 11140:GOTO 12000' Up & left
11100 IF ROW=1 THEN 12010' Up?
11110 ROW=ROW-1:RETURN
11120 IF ROW=ROWMAX THEN 12010' Down?
11130 ROW=ROW+1:RETURN
11140 IF COL-PIXSIZ<1 THEN 12010' Left?
11150 COL=COL-PIXSIZ:RETURN
11160 IF COL+PIXSIZ>COLMAX THEN 12010' Right?
11170 COL=COL+PIXSIZ:RETURN
11999 '**************  Position cursor on scren  *************
12000 LOCATE ROW,COL,0:RETURN
12010 BEEP:RETURN
13000 GOSUB 12000:COLOR CLRTAB(CURCLR),BCKGRD(BCKGRD):PRINT STRING$(PIXSIZ,DENSITY(DENSITY));:ACTIVE=-1:RETURN
19999 '************  Determine screen attributes  *************
20000 CHAR(Z)=SCREEN(ROW,COL):ATTRIB=SCREEN(ROW,COL,1)
20010 SAVCLR(Z)=(ATTRIB MOD 16):SAVBCK(Z)=(((ATTRIB-SAVCLR(Z))/16)MOD 128):RETURN' Save screen attributes
20200 ROW=25:FOR Z=1 TO PIXMAX:COL=Z:GOSUB 20000:NEXT:Z=0:RETURN' Save row
20400 ROW=25:FOR Z=1 TO PIXMAX:COL=Z:GOSUB 21000:NEXT:Z=0:RETURN' Restore row
21000 GOSUB 12000:COLOR SAVCLR(Z),SAVBCK(Z):PRINT CHR$(CHAR(Z));:RETURN' Restore screen values
21999 '************  Save picture space to a file  ************
22000 GOSUB 24000:IF NOT FOUND THEN 22020
22010 GOSUB 20200:LOCATE 25,1:COLOR HIGH,0:INPUT;"PICTURE ALREADY EXISTS - SAVE OVER IT (Y/N)";Q$:GOSUB 20400:IF Q$="Y" OR Q$="y" THEN KILL FILNAM$ ELSE GOSUB 31500:RETURN
22020 GOSUB 22100:GOSUB 31500:RETURN
22100 DEF SEG=&HB800:BSAVE FILNAM$,0,&H4000:DEF SEG:RETURN
22999 '***********  Load picture space from a file  ***********
23000 GOSUB 29000:GOSUB 24000:IF FOUND THEN ACTIVE=-1:SAVED=0:GOTO 23100
23010 GOSUB 20200:LOCATE 25,1:COLOR HIGH,0:INPUT;"PICTURE NOT FOUND - PRESS 'ENTER' ",Q$:GOSUB 20400:GOTO 23110
23100 DEF SEG=&HB800:BLOAD FILNAM$,0:DEF SEG
23110 GOSUB 31500:RETURN
23999 '**************  Get file drive and name  ***************
24000 LOCATE 23,72:COLOR HIGH:INPUT;"",Q$:IF Q$="" THEN 24020' Use default
24010 Q=ASC(Q$):IF (Q>64 AND Q<91) OR (Q>96 AND Q<123) THEN FILDRV$=CHR$(Q) ELSE BEEP:GOTO 24000' A-Z, upper and lower case
24020 LOCATE 24,71:INPUT;"",Q$:IF LEN(Q$)>8 THEN BEEP:GOTO 24020
24030 IF Q$<>"" THEN NAMFIL$=Q$' Use default name?
24040 COLOR LOW:FILNAM$=FILDRV$+":"+NAMFIL$+".PIX"
24999 '***********  Test for whether a file exists  ***********
25000 FOUND=0:ON ERROR GOTO 25010:NAME FILNAM$ AS FILNAM$:GOTO 25030
25010 IF ERR=58 THEN FOUND=-1
25020 RESUME 25030
25030 ON ERROR GOTO 26000:RETURN
25999 '*******************  File error trap  ******************
26000 GOSUB 20200:LOCATE 25,1:COLOR HIGH,0
26010 IF ERR=61 OR ERR=67 OR (ERR>69 AND ERR<73) THEN PRINT "DISK ERROR, RETRY ON ANOTHER DISK"; ELSE PRINT "UNKNOWN ERROR:";ERR;"ON LINE:";ERL;
26020 INPUT;" - PRESS 'ENTER' ",Q$:GOSUB 20400:GOSUB 31500:RESUME 500
26999 '*******************  Save temp file  *******************
27000 FILNAM$=FILDRV$+":TEMP.PIX":GOSUB 25000:IF FOUND THEN KILL FILNAM$
27010 GOSUB 22100:RETURN
27999 '*******************  Load temp file  *******************
28000 FILNAM$=FILDRV$+":TEMP.PIX":GOSUB 25000:IF FOUND THEN 23100
28010 GOSUB 31500:RETURN
28999 '**************  Picture not saved warning  *************
29000 IF ACTIVE THEN GOSUB 20200:LOCATE 25,1:COLOR HIGH,0:INPUT;"DO YOU WANT TO SAVE THE CURRENT PICTURE (Y/N)";Q$:GOSUB 20400:IF Q$="Y" OR Q$="y" THEN GOSUB 2200
29010 RETURN
29999 '*******************  Help statements  ******************
30000 IF ACTIVE THEN GOSUB 27000
30010 COLOR LOW,0:RESTORE 30500:COL=1:FOR ROW=1 TO ROWMAX:READ ROW$:GOSUB 12000:IF ROW=25 THEN COLOR HIGH
30020 PRINT ROW$;SPC(PIXMAX-LEN(ROW$));:NEXT:LOCATE 24,32:Q$=INPUT$(1):IF ACTIVE THEN GOSUB 28000 ELSE GOSUB 31000
30030 RETURN
30500 DATA ""
30510 DATA "                                 Up-left    Up     Up-right"
30520 DATA "    This is the Picture Space         7      8      9"
30530 DATA ""
30540 DATA "          Cursor Keys:                  Select Key"
30550 DATA " NUM LOCK must be ON to move.    Left 4      5      6 Right"
30560 DATA " ALT and any of these keys will"
30570 DATA " move the cursor all the way to"
30580 DATA " the edge of the picture space.       1      2      3"
30590 DATA " Press SELECT to put a pixel at  Down-left Down  Down-right"
30600 DATA " the current cursor position in"
30610 DATA " the current color, density and background.  ALT plus the"
30620 DATA " SELECT key will center the cursor.  Press SELECT anywhere"
30630 DATA " outside the picture space to pick colors or other options."
30640 DATA ""
30650 DATA "                    Function Keys:"
30660 DATA "   F1 Pick higher color       F  Fill area with color"
30670 DATA "   F2 Pick lower color        C  Clear picture space"
30680 DATA "   F3 Pick higher density     U  Undo last change"
30690 DATA "   F4 Pick lower density      L  Load a picture file"
30700 DATA "   F5 Switch background       S  Save a picture file"
30710 DATA "   H  Display help screen     E  End the program"
30720 DATA ""
30730 DATA "     Press any key to continue:"
30740 DATA "FATPIXS     Copyright 1984 (c) by CompuQuill, Atlanta, Georgia"
30999 '****************  Clear & center cursor  ***************
31000 GOSUB 29000:COLOR 7,0:COL=1:FOR ROW=1 TO ROWMAX:GOSUB 12000:PRINT SPC(PIXMAX);:NEXT:ACTIVE=0:SAVED=0
31500 CURCLR=3:BCKGRD=0:DENSITY=0:GOSUB 10100:GOSUB 3000:RETURN
32999 '********************  Fill Region  *********************
33000 IF ACTIVE THEN GOSUB 27000:SAVED=-1
33010 INDEX=0:ROW=LASTROW:COL=LASTCOL:GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN RETURN ELSE GOSUB 33500' Starting run
33100 UPRUN=0:DNRUN=0:ROW=ROW(INDEX):COL=COL(INDEX):INDEX=INDEX-1:FOR COL=COL TO 1 STEP -1:GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN 33120
33110 GOSUB 13000:GOSUB 33200:NEXT' Fill & check adjacent rows
33120 IF INDEX=0 THEN ROW=LASTROW:COL=LASTCOL:RETURN ELSE 33100' Last run?
33200 HLDCOL=COL:IF ROW=ROWMAX THEN 33240 ELSE ROW=ROW+1' Test run below, above
33210 GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN DNRUN=0:GOTO 33230
33220 IF DNRUN=0 THEN GOSUB 33500:DNRUN=-1
33230 COL=HLDCOL:ROW=ROW-1
33240 IF ROW=1 THEN RETURN ELSE ROW=ROW-1
33250 GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN UPRUN=0:GOTO 33270
33260 IF UPRUN=0 THEN GOSUB 33500:UPRUN=-1
33270 COL=HLDCOL:ROW=ROW+1:RETURN
33500 GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN RETURN
33510 FOR COL=COL TO PIXMAX:GOSUB 20000:IF SAVCLR(Z)=CLRTAB(CURCLR) THEN 33530
33520 NEXT
33530 IF INDEX>64 THEN GOSUB 20200:LOCATE 25,1:COLOR HIGH,0:INPUT;"FILL IS TOO COMPLEX - PRESS 'ENTER' ",Q$:GOSUB 20400:GOSUB 31500:RETURN
33540 INDEX=INDEX+1:ROW(INDEX)=ROW:COL(INDEX)=COL-1:RETURN
33999 '******************  Undo processing  *******************
34000 IF SAVED THEN GOSUB 28000:SAVED=0:GOSUB 31500:RETURN
34010 IF ACTIVE THEN Z=65:ROW=ROW(Z):COL=COL(Z):GOSUB 21000:Z=0
34020 GOSUB 3000:RETURN

